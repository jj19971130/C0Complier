C-0文法

1.关键字: const, int, void, if, else, while, main, return, printf, scanf
＜加法运算符＞  ::=  +｜－
＜乘法运算符＞ ::=  * ｜／
＜关系运算符＞ ::=  ＜｜＜＝｜＞｜＞＝｜!＝｜＝＝
＜字符＞   ::=  ＿｜a｜．．．｜z｜A｜．．．｜Z
＜数字＞ ::=  0｜＜非零数字＞
＜非零数字＞    ::=  1｜．．．｜9
＜专用符号＞ ::=  (｜)｜{｜}｜，｜；｜＝
<字符串> ::= "｛<合法字符> }"

2.
A＜程序＞ ::=  〔＜常量说明部分＞〕〔＜变量说明部分＞〕｛＜函数定义部分＞｝＜主函数＞
B＜常量说明部分＞  ::=  const ＜常量定义＞｛,＜常量定义＞｝;
C＜常量定义＞  ::=  ＜标识符＞＝＜整数＞
D＜整数＞ ::=  〔＋｜－〕＜非零数字＞｛＜数字＞｝｜０
E＜标识符＞ ::=  ＜字符＞｛＜字符＞｜＜数字＞｝
F＜声明头部＞ ::=  int　＜标识符＞
G＜变量说明部分＞ ::=  ＜声明头部＞｛，＜标识符＞｝；
H＜函数定义部分＞ ::=  （＜声明头部＞｜void ＜标识符＞）＜参数＞＜复合语句＞
I＜复合语句＞ ::=  ‘{’〔＜常量说明部分＞〕〔＜变量说明部分＞〕＜语句序列＞‘}’
J＜参数＞ ::=  ‘(’＜参数表＞‘)’
K＜参数表＞ ::=  int ＜标识符＞｛，int ＜标识符＞} | 空
                  //参数表可以为空
L＜主函数＞ ::=  ( void ｜int ) main ＜参数＞＜复合语句＞
M＜表达式＞ ::=  〔＋｜－〕＜项＞｛＜加法运算符＞＜项＞｝
N＜项＞ ::=  ＜因子＞{＜乘法运算符＞＜因子＞}
O＜因子＞ ::=  ＜标识符＞｜‘（’＜表达式＞‘）’｜＜整数＞｜＜函数调用语句＞
P＜语句＞ ::= ＜条件语句＞｜＜循环语句＞｜‘{’<语句序列>‘}’｜＜函数调用语句＞;｜＜赋值语句＞; | <返回语句>;｜＜读语句＞;｜＜写语句＞;｜＜空＞
Q＜赋值语句＞ ::=  ＜标识符＞＝＜表达式＞
R＜条件语句＞ ::=  if‘（’＜条件＞‘）’＜语句＞〔else＜语句＞〕
S＜条件＞ ::=  ＜表达式＞＜关系运算符＞＜表达式＞｜＜表达式＞
T＜循环语句＞ ::=  while‘（’＜条件＞‘）’＜语句＞
U＜函数调用语句＞ ::=  ＜标识符＞‘（’＜值参数表＞‘）’
V＜值参数表＞ ::=  ＜表达式＞｛，＜表达式＞｝｜＜空＞
W＜语句序列＞ ::=  ＜语句＞｛＜语句＞｝
X＜读语句＞ ::=  scanf‘(’＜标识符＞‘）’
Y＜写语句＞ ::=  printf‘(’[<字符串>,][＜expression ＞]‘）’
　　　　　 //当出现字符串时，就加印字符串, 之后打印表达式的值；
Z＜返回语句＞ ::=  return [ ‘(’＜表达式＞’)’ ]
